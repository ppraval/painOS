alias functionNum R1;
alias currentPID R2;

//swap out function 
if (functionNum == SWAP_OUT) then
    alias counter R5;
    counter = 3; 

    alias waitSemPID R10;
    waitSemPID = -1;
    
    alias maxTickPID R11;
    maxTickPID = -1;

    alias maxTICK R12;
    maxTICK = -1;

    alias swapPID R3;
    swapPID = -1;

    while (counter < MAX_PROC_NUM - 1) do
        //we are checking the swap flag and if there is no swap then we swap the process that is waiting
        if ([PROCESS_TABLE + counter*16 + 6] == 0) then
            if ([PROCESS_TABLE + counter*16 + 4] == WAIT_PROCESS) then
                swapPID = counter;
                break;
            endif;
            // or swap out the process that is in wait semaphore
            if (waitSemPID == -1 && [PROCESS_TABLE + counter*16 + 4] == WAIT_SEMAPHORE) then
                waitSemPID = counter;
            endif;

            alias tempPCB R13;
            tempPCB = PROCESS_TABLE + counter*16;
            // checking the tick field of the process table
            if ([tempPCB+4] != TERMINATED && [tempPCB+4] != ALLOCATED && [tempPCB] != RUNNING) then
                // if the tick is more then max tick then we swap out the process
                if ([tempPCB] > maxTICK) then
                    maxTICK = [tempPCB];
                    maxTickPID = counter;
                endif;
            endif;
        endif;
        counter = counter + 1;
    endwhile;

    //we swap out the process that is waiting if not we check ticks
    if (swapPID == -1) then
        if (waitSemPID != -1) then
            swapPID = waitSemPID;
        else
            swapPID = maxTickPID;
        endif;
    endif;

    // if there is no process to be swapped out then we stop the swapping process 
    if (swapPID == -1) then
        [SYSTEM_STATUS_TABLE + 5] = 0;
        R0 = -1; // and return -1
        // breakpoint;
        return;
    endif;
    // setting the tick to 0
    [PROCESS_TABLE + swapPID*16] = 0;

    alias swapPTBR R4;
    swapPTBR = [PROCESS_TABLE + swapPID*16 + 14];
    // looping to check the pages
    counter = 4;
    while (counter < 20) do
        alias pageNumber R6;
        pageNumber = [swapPTBR + counter];

        // heap pages or stack pages
        if (counter <= 6 || counter >= 16) then
            // if page is not invalid
            if (pageNumber != -1 && [MEMORY_FREE_LIST + pageNumber] == 1) then
                multipush (R1, R2, R3, R4, R5, R6);

                //swapping out the page into a block
                R1 = GET_SWAP_BLOCK;
                call MEMORY_MANAGER;
                multipop  (R1, R2, R3, R4, R5, R6);

                [DISK_MAP_TABLE + 10*swapPID + (counter/2)] = R0;

                multipush (R1, R2, R3, R4, R5, R6);
                // storing the page to disk allocaed by the above function
                R1 = DISK_STORE;
                R2 = SWAPPER_DAEMON;
                R3 = pageNumber;
                R4 = R0;
                call DEVICE_MANAGER;
                multipop  (R1, R2, R3, R4, R5, R6);
            endif;
        endif;
        if (pageNumber != -1) then
            multipush (R1, R3, R3, R5);

            // calling release page to release the mem pages
            R1 = RELEASE_PAGE;
            R2 = pageNumber;

            call MEMORY_MANAGER;
            multipop  (R1, R3, R3, R5);
        endif;
        // invalidating all the pages
        [swapPTBR + counter] = -1;
        [swapPTBR + counter + 1] = "0000";

        counter = counter + 2;
    endwhile;
    // setting swap flag to 1
    [PROCESS_TABLE + swapPID*16 + 6] = 1;
    // increment the swapped count
    [SYSTEM_STATUS_TABLE+4] = [SYSTEM_STATUS_TABLE+4] + 1;
    // resetting the paging status 
    [SYSTEM_STATUS_TABLE+5] = 0;

    // print "swap out";
    // print swapPID;

    // breakpoint;
    return;
endif;

if (functionNum == SWAP_IN) then
    alias counter R5;
    counter = 3;

    alias swapPID R3;
    swapPID = -1;
    alias maxTICK R10;
    maxTICK = 0;
    // looping to find the process with the highest tick among the swapped out ready processes
    while (counter < MAX_PROC_NUM - 1) do
        if ([PROCESS_TABLE + counter * 16 + 6] == 1 && [PROCESS_TABLE + counter * 16 + 4] == READY) then
            if ([PROCESS_TABLE + counter * 16] > maxTICK) then
                swapPID = counter;
                maxTICK = [PROCESS_TABLE + counter * 16];
            endif;
        endif;
        counter = counter + 1;
    endwhile;

    if (swapPID == -1) then
        // reset the paging status
        [SYSTEM_STATUS_TABLE + 5] = 0;
        R0 = -1;
        // breakpoint;
        return;
    endif;

    [PROCESS_TABLE + swapPID * 16] = 0;

    alias swapPTBR R4;
    swapPTBR = [PROCESS_TABLE + swapPID*16 + 14];
    // looping to check the pages
    counter = 4;
    while (counter < 20) do
        // heap pages or stack pages
        if (counter >= 8 && counter < 16) then
            counter = counter + 2;
            continue;
        endif;

        alias blockNumber R6;
        blockNumber = [DISK_MAP_TABLE + swapPID*10 + (counter/2)];
        //if block is valid
        if (blockNumber != -1) then
            multipush (R1, R2, R3, R4, R5, R6);
            // we allocate free mem pages
            R1 = GET_FREE_PAGE;
            call MEMORY_MANAGER;
            multipop  (R1, R2, R3, R4, R5, R6);

            // allocating free memory pages to heap and stack
            [swapPTBR + counter] = R0;
            [swapPTBR + counter + 1] = "0110";
            if (counter >= 8 && counter < 16) then
                [swapPTBR + counter + 1] = "0100";
            endif;

            multipush (R1, R2, R3, R4, R5, R6);
            //loading the disk into the pages and
            R1 = DISK_LOAD;
            R2 = SWAPPER_DAEMON;
            R3 = R0;
            R4 = blockNumber;

            call DEVICE_MANAGER;
            multipop  (R1, R2, R3, R4, R5, R6);

            multipush (R1, R2, R3, R4, R5, R6);
            // releasing the block after loading
            R1 = RELEASE_BLOCK;
            R2 = blockNumber;
            R3 = SWAPPER_DAEMON;

            call MEMORY_MANAGER;
            multipop   (R1, R2, R3, R4, R5, R6);
            // invalidating the disk
            [DISK_MAP_TABLE + swapPID*10 + (counter/2)] = -1;
        endif;
        counter = counter + 2;
    endwhile;

    [PROCESS_TABLE + swapPID*16 + 6] = 0;

    [SYSTEM_STATUS_TABLE+4] = [SYSTEM_STATUS_TABLE+4] - 1;
    [SYSTEM_STATUS_TABLE+5] = 0;

    // print "swap in";
    // print swapPID;

    // breakpoint;
    return;
endif;